<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.12">
  <POU Name="FB_Station_Disable" Id="{f250b27e-a3a3-4f8c-a728-76a9ef965b85}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Station_Disable EXTENDS FB_CH_Object
VAR
	ipStation : I_XP_Station_PLC;
	sTargetStation : STRING; //the mover gets this as next target
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{fcb0a98d-d756-49b7-8bce-33c65cdfba9e}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	
	ipStation : I_XP_Station_PLC;
	
END_VAR
VAR
	fbGetCurTaskIdx  : GETCURTASKINDEX;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.ipStation := ipStation;]]></ST>
      </Implementation>
    </Method>
    <Property Name="TargetStation" Id="{5ff39824-8aa6-4379-85ea-5b936c3fe971}">
      <Declaration><![CDATA[{attribute 'monitoring':='call'}
 //the mover gets this as next target
PROPERTY TargetStation : STRING]]></Declaration>
      <Get Name="Get" Id="{97b60edd-bb37-40e4-8720-74ff66e6d2e0}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[TargetStation := sTargetStation;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{0aff3268-9492-4c82-bae7-14cc2d4fc454}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[sTargetStation := TargetStation;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="Update" Id="{109ef4a0-0354-43ed-b4f8-08e59b74708c}">
      <Declaration><![CDATA[METHOD Update : HRESULT
VAR
	refPlanarTrack : REFERENCE TO MC_PlanarTrack;
END_VAR

VAR_INST
	nSeq : UINT;
	fbFeedback : MC_PlanarFeedback;
	fbTon : TON;
	ipMoverInStation : I_XP_Mover_PLC;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF ipStation = 0 THEN
	RETURN; // ERROR
END_IF

IF ipStation.System.Disabled THEN
	nSeq := 0;
END_IF

ipStation.SimulationStop();

CASE nSeq OF 

	0: //wait for mover
		IF ipStation.MoversInPlace THEN
			ipMoverInStation := ipStation.MoversInQueue[0];
			nSeq := 1;
		END_IF
		
	1: //disable
		ipMoverInStation.PlanarMover.Disable(fbFeedback);
		nSeq := 2;				

	2: // wait until disable is finish
		IF fbFeedback.Done THEN
			fbTon.IN := FALSE;
			nSeq := 3;	
		END_IF
		
	3: //Delay 5s
		fbTon.IN := TRUE;
		IF fbTon.q THEN
			nSeq := 4;	
		END_IF			

	4: //enable
		ipMoverInStation.PlanarMover.Enable(fbFeedback);
		nSeq := 5;				

	5: // wait until enable is finish
		IF fbFeedback.Done THEN
			fbTon.IN := FALSE;
			nSeq := 6;	
		END_IF
		
	6: //join Track
		refPlanarTrack REF= ipStation.AccocietedTrack.PlanarTrack;
		ipMoverInStation.PlanarMover.JoinTrack(	commandFeedback	:= fbFeedback, 
												targetTrack		:= refPlanarTrack, 
												constraint		:= ipMoverInStation.DynConstraint, 
												options			:= 0);
		nSeq := 7;				

	7: // wait until enable is finish
		IF fbFeedback.Done THEN
			fbTon.IN := FALSE;
			nSeq := 8;	
		END_IF
		
	8: // send movers away
		ipStation.SendMoversToStation(THIS^.sTargetStation);
		nSeq := 0;	
	
END_CASE
fbFeedback.Update();
fbTon(PT:= T#5S);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>